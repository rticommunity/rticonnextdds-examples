/*****************************************************************************/
/*         (c) Copyright, Real-Time Innovations, All rights reserved.        */
/*                                                                           */
/*         Permission to modify and use for internal purposes granted.       */
/* This software is provided "as is", without warranty, express or implied.  */
/*                                                                           */
/*****************************************************************************/

module PrimeCalculator {

/* 
 * This is the request type. 
 *
 * It simply contains an integer that requests all the prime numbers below it.
 *
 */
struct PrimeNumberRequest {
        /*
         * Requests the calculation of the prime numbers below n
         */
        long n;
        
        /*
         * How many prime numbers should be included in each reply sample.
         * 
         * A smaller value means that results are received more frequently
         * (and hence more data samples are sent)
         */
        long primes_per_reply;
};

/* --- Reply type: --------------------------------------------------------- */

/*
 * Maximum size of the a sequence of prime numbers
 */
const long PRIME_SEQUENCE_MAX_LENGTH = 1024;


/*
 * Indicates the status of one of the multiple possible replies for a request
 */
enum PrimeNumberCalculationStatus {
        /*
         * Indicates that this reply contains a new sequence of
         * prime numbers for a request, but there are still more to come
         */
        REPLY_IN_PROGRESS,
        /*
         * Indicates that this is the last sequence of 
         * prime numbers for a request.
         */
        REPLY_COMPLETED,
        /*
         * Indicates that there was an error. After an error
         * there won't be any more replies for a request
         */
        REPLY_ERROR
};

/*
 * This is the reply type. 
 * 
 * The replier sends sequences of prime numbers as they are being calculated.
 *  
 */
struct PrimeNumberReply {
        /*
         * A sequence of prime numbers
         */
        sequence<long, PRIME_SEQUENCE_MAX_LENGTH> primes;
        
        /*
         * Status information about this reply
         */
         PrimeNumberCalculationStatus status;
};

};